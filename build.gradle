plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.freefair.lombok' version '8.6'
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://jitpack.io')
    }

    maven {
        url = uri('https://repo.dmulloy2.net/repository/public/')
    }

    maven {
        url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/')
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'com.github.Slimefun:Slimefun4:da9c2ac4cc'
    implementation 'net.guizhanss:GuizhanLib-api:1.7.4'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.1.0'
}

group = 'net.guizhanss'
version = 'UNOFFICIAL'
description = 'SlimefunTranslation'

java {
    sourceCompatibility = JavaVersion.VERSION_16
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

shadowJar {
    relocate 'org.bstats', 'net.guizhanss.slimefuntranslation.libs.bstats'
    relocate 'net.guizhanss.guizhanlib', 'net.guizhanss.slimefuntranslation.libs.guizhanlib'
    minimize()
    archiveClassifier = ''
}

bukkit {
    main = 'net.guizhanss.slimefuntranslation.SlimefunTranslation'
    apiVersion = '1.16'
    authors = ['ybw0014']
    description = 'A Slimefun Addon that translates items without actually modifying the items.'
    depend = ['Slimefun', 'ProtocolLib', 'PlaceholderAPI']
    softDepend = ['GuizhanLibPlugin']

    commands {
        sftranslation {
            description = 'SlimefunTranslation command'
            aliases = ['slimefuntranslation', 'sft', 'sftr', 'sftransl']
        }
    }

    permissions {
        'sftranslation.command.id' {
            description = 'Get the ID of the Slimefun item in your main hand'
            setDefault('TRUE')
        }
        'sftranslation.command.search' {
            description = 'Search for a Slimefun item with your current language'
            setDefault('TRUE')
        }
        'sftranslation.command.translation.extract' {
            description = 'Extract the translation files to the "translations" folder'
            setDefault('OP')
        }
        'sftranslation.command.translation.generate' {
            description = 'Generate a translation file based on the given addon and language.'
            setDefault('OP')
        }
        'sftranslation.command.translation.reload' {
            description = 'Reload the translation files.'
            setDefault('OP')
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}
